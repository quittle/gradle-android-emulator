buildscript {
    final Map<String, String> env = System.getenv()
    project.ext.ANDROID_GRADLE_VERSION = env.getOrDefault('ANDROID_GRADLE_VERSION', '3.2.1')
    project.ext.ANDROID_GRADLE_VERSION_NEW = Boolean.valueOf(env.getOrDefault('ANDROID_GRADLE_VERSION_NEW', 'true'))
    project.ext.ANDROID_EMULATOR_SDK_VERSION = Integer.valueOf(env.getOrDefault('ANDROID_EMULATOR_SDK_VERSION', '21'))
    project.ext.ANDROID_EMULATOR_GOOGLE_APIS = Boolean.valueOf(env.getOrDefault('ANDROID_EMULATOR_GOOGLE_APIS', 'false'))
    project.ext.ANDROID_EMULATOR_ABI = env.getOrDefault('ANDROID_EMULATOR_ABI', 'armeabi-v7a')
    project.ext.ENABLE_FOR_ANDROID_TESTS = Boolean.valueOf(env.getOrDefault('ENABLE_FOR_ANDROID_TESTS', 'true'))
    project.ext.LOG_ANDROID_EMULATOR = Boolean.valueOf(env.getOrDefault('LOG_ANDROID_EMULATOR', 'false'))
    project.ext.FAIL_EMULATOR_STARTUP = Boolean.valueOf(env.getOrDefault('FAIL_EMULATOR_STARTUP', 'false'))
    project.ext.ANDROID_EMULATOR_DEVICE = env.getOrDefault('ANDROID_EMULATOR_DEVICE', null)

    repositories {
        mavenLocal()
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.quittle:android-emulator:+'
        classpath "com.android.tools.build:gradle:${ANDROID_GRADLE_VERSION}"
    }
}

plugins {
    id 'com.quittle.setup-android-sdk' version '2.0.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.quittle.android-emulator'

allprojects {
    repositories {
        google()
        jcenter()
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 14
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    if (ANDROID_GRADLE_VERSION_NEW) {
        testOptions {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
    }

    useLibrary 'android.test.runner'
}

dependencies {
    if (ANDROID_GRADLE_VERSION_NEW) {
        androidTestImplementation 'androidx.test:runner:1.1.1'
        androidTestImplementation 'androidx.test.ext:junit:1.1.0'
        androidTestUtil 'androidx.test:orchestrator:1.1.1'
    } else {
        androidTestCompile 'androidx.test:runner:1.1.1'
        androidTestCompile 'androidx.test.ext:junit:1.1.0'
    }
}

androidEmulator {
    emulator {
        sdkVersion ANDROID_EMULATOR_SDK_VERSION
        sdkVersion = ANDROID_EMULATOR_SDK_VERSION

        abi ANDROID_EMULATOR_ABI
        abi = ANDROID_EMULATOR_ABI

        includeGoogleApis ANDROID_EMULATOR_GOOGLE_APIS
        includeGoogleApis = ANDROID_EMULATOR_GOOGLE_APIS

        device ANDROID_EMULATOR_DEVICE
        device = ANDROID_EMULATOR_DEVICE
    }

    enableForAndroidTests ENABLE_FOR_ANDROID_TESTS
    enableForAndroidTests = ENABLE_FOR_ANDROID_TESTS

    headless true
    headless = true

    additionalEmulatorArguments '-show-kernel', '-verbose'
    additionalEmulatorArguments = ['-show-kernel', '-verbose']

    if (FAIL_EMULATOR_STARTUP) {
        additionalEmulatorArguments = ['fake', 'arguments']
    }

    logEmulatorOutput LOG_ANDROID_EMULATOR
    logEmulatorOutput = LOG_ANDROID_EMULATOR
}
