buildscript {
    final Map<String, String> env = System.getenv()
    project.ext.ANDROID_GRADLE_VERSION = env.getOrDefault('ANDROID_GRADLE_VERSION', '7.0.0')
    project.ext.ANDROID_EMULATOR_SDK_VERSION = Integer.valueOf(env.getOrDefault('ANDROID_EMULATOR_SDK_VERSION', '21'))
    project.ext.ANDROID_EMULATOR_GOOGLE_APIS = Boolean.valueOf(env.getOrDefault('ANDROID_EMULATOR_GOOGLE_APIS', 'false'))
    project.ext.ANDROID_EMULATOR_ABI = env.getOrDefault('ANDROID_EMULATOR_ABI', 'x86')
    project.ext.ENABLE_FOR_ANDROID_TESTS = Boolean.valueOf(env.getOrDefault('ENABLE_FOR_ANDROID_TESTS', 'true'))
    project.ext.LOG_ANDROID_EMULATOR = Boolean.valueOf(env.getOrDefault('LOG_ANDROID_EMULATOR', 'false'))
    project.ext.FAIL_EMULATOR_STARTUP = Boolean.valueOf(env.getOrDefault('FAIL_EMULATOR_STARTUP', 'false'))
    project.ext.ANDROID_EMULATOR_DEVICE = env.getOrDefault('ANDROID_EMULATOR_DEVICE', null)

    repositories {
        mavenLocal()
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.quittle:android-emulator:+'
        classpath "com.android.tools.build:gradle:${ANDROID_GRADLE_VERSION}"
    }
}

plugins {
    id 'com.quittle.setup-android-sdk' version '2.0.3'
}

apply plugin: 'com.android.application'
apply plugin: 'com.quittle.android-emulator'

allprojects {
    repositories {
        google()
        jcenter()
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 14
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    useLibrary 'android.test.runner'
}

dependencies {
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestUtil 'androidx.test:orchestrator:1.3.0'
}

androidEmulator {
    emulator {
        sdkVersion ANDROID_EMULATOR_SDK_VERSION
        sdkVersion = ANDROID_EMULATOR_SDK_VERSION

        abi ANDROID_EMULATOR_ABI
        abi = ANDROID_EMULATOR_ABI

        includeGoogleApis ANDROID_EMULATOR_GOOGLE_APIS
        includeGoogleApis = ANDROID_EMULATOR_GOOGLE_APIS

        device ANDROID_EMULATOR_DEVICE
        device = ANDROID_EMULATOR_DEVICE
    }

    enableForAndroidTests ENABLE_FOR_ANDROID_TESTS
    enableForAndroidTests = ENABLE_FOR_ANDROID_TESTS

    headless true
    headless = true

    additionalEmulatorArguments '-show-kernel', '-verbose'
    additionalEmulatorArguments = ['-show-kernel', '-verbose']

    if (FAIL_EMULATOR_STARTUP) {
        additionalEmulatorArguments = ['fake', 'arguments']
    }

    logEmulatorOutput LOG_ANDROID_EMULATOR
    logEmulatorOutput = LOG_ANDROID_EMULATOR
}

com.android.ddmlib.DdmPreferences.setTimeOut(30000)
