buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:5.2.1.201812262042-r'
    }
}

import org.eclipse.jgit.api.Git

plugins {
    id 'findbugs'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'maven-publish'
    id 'pmd'
}

repositories {
    google()
    jcenter()
}

group = 'com.quittle'
version = Git.open(project.rootDir.parentFile)
        .describe()
        .setTarget("HEAD")
        .setTags(true)
        .call()

gradlePlugin {
    plugins {
        androidEmulatorPlugin {
            id = 'com.quittle.android-emulator'
            implementationClass = 'com.quittle.androidemulator.AndroidEmulatorPlugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'android-emulator'
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/quittle/gradle-android-emulator'
    vcsUrl = 'https://github.com/quittle/gradle-android-emulator'
    description = 'Automatically installs and sets up the Android emulator for easy running of instrumentation tests.'
    tags = ['android', 'android emulator', 'emulator', 'automation', 'instrumentation']

    plugins {
        androidEmulatorPlugin {
            displayName = 'Automated Android Emulator for easing development and testing'
        }
    }
}

dependencies {
    implementation 'com.android.tools.build:gradle:3.3.1'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.logging.log4j:log4j-iostreams:2.11.2'

    compileOnly 'com.google.code.findbugs:annotations:3.0.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all' << '-Werror' << '-proc:none'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

javadoc {
    failOnError true
    options.addStringOption 'Xwerror', '-quiet' // See https://stackoverflow.com/a/49544352
}

findbugs {
    effort 'max'
    reportLevel 'low'
}

defaultTasks 'assemble', 'javadoc', 'check'
