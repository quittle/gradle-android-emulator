buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:6.4.0.202211300538-r'
    }
}

import com.github.spotbugs.snom.SpotBugsTask
import org.eclipse.jgit.api.Git

plugins {
    id 'com.github.spotbugs' version '5.0.13'
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'pmd'
}

repositories {
    google()
    mavenCentral()
}

group = 'com.quittle'
version = Git.open(project.rootDir.parentFile)
        .describe()
        .setTarget("HEAD")
        .setTags(true)
        .call()

gradlePlugin {
    plugins {
        androidEmulatorPlugin {
            id = 'com.quittle.android-emulator'
            implementationClass = 'com.quittle.androidemulator.AndroidEmulatorPlugin'
            displayName = 'Automated Android Emulator for easing development and testing'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'android-emulator'
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/quittle/gradle-android-emulator'
    vcsUrl = 'https://github.com/quittle/gradle-android-emulator'
    description = 'Automatically installs and sets up the Android emulator for easy running of instrumentation tests.'
    tags = ['android', 'android emulator', 'emulator', 'automation', 'instrumentation']
}

dependencies {
    compileOnly 'com.android.tools:common:30.3.1'
    def ANDROID_GRADLE_PLUGIN_VERSION = '7.3.1'
    compileOnly "com.android.tools.build:gradle:$ANDROID_GRADLE_PLUGIN_VERSION"
    implementation 'commons-io:commons-io:2.8.0'

    compileOnly 'com.google.code.findbugs:annotations:3.0.1'
    testCompileOnly 'com.google.code.findbugs:annotations:3.0.1'

    testImplementation 'io.mockk:mockk:1.13.2'
    testImplementation "com.android.tools.build:gradle:$ANDROID_GRADLE_PLUGIN_VERSION"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.9.0'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all' << '-Werror' << '-proc:none'
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

javadoc {
    failOnError true
    options.addStringOption 'Xwerror', '-quiet' // See https://stackoverflow.com/a/49544352

    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
        options.addBooleanOption 'html5', true
    }
}

spotbugs {
    effort 'max'
    reportLevel 'low'
    excludeFilter = file('configuration/spotbugs-exclude.xml')
}

pmd {
    ruleSetFiles = files("configuration/pmd.xml")
    consoleOutput true
    ruleSets = []
}

defaultTasks 'assemble', 'javadoc', 'check'
