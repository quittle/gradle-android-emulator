buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:5.8.1.202007141445-r'
    }
}

import com.github.spotbugs.snom.SpotBugsTask
import org.eclipse.jgit.api.Git

plugins {
    id 'com.github.spotbugs' version '4.4.4'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'pmd'
}

repositories {
    google()
    jcenter()
}

group = 'com.quittle'
version = Git.open(project.rootDir.parentFile)
        .describe()
        .setTarget("HEAD")
        .setTags(true)
        .call()

gradlePlugin {
    plugins {
        androidEmulatorPlugin {
            id = 'com.quittle.android-emulator'
            implementationClass = 'com.quittle.androidemulator.AndroidEmulatorPlugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'android-emulator'
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/quittle/gradle-android-emulator'
    vcsUrl = 'https://github.com/quittle/gradle-android-emulator'
    description = 'Automatically installs and sets up the Android emulator for easy running of instrumentation tests.'
    tags = ['android', 'android emulator', 'emulator', 'automation', 'instrumentation']

    plugins {
        androidEmulatorPlugin {
            displayName = 'Automated Android Emulator for easing development and testing'
        }
    }
}

dependencies {
    implementation 'com.android.tools.build:gradle:4.0.1'
    implementation 'commons-io:commons-io:2.7'
    implementation 'org.apache.logging.log4j:log4j-iostreams:2.13.3'

    compileOnly 'com.google.code.findbugs:annotations:3.0.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all' << '-Werror' << '-proc:none'
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

javadoc {
    failOnError true
    options.addStringOption 'Xwerror', '-quiet' // See https://stackoverflow.com/a/49544352

    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
        options.addBooleanOption 'html5', true
    }
}

spotbugs {
    effort 'max'
    reportLevel 'low'
}

defaultTasks 'assemble', 'javadoc', 'check'
