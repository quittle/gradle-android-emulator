buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:6.2.0.202206071550-r'
    }
}

import com.github.spotbugs.snom.SpotBugsTask
import org.eclipse.jgit.api.Git

plugins {
    id 'com.github.spotbugs' version '5.0.9'
    id 'com.gradle.plugin-publish' version '0.21.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'pmd'
}

repositories {
    google()
    mavenCentral()
}

group = 'com.quittle'
version = Git.open(project.rootDir.parentFile)
        .describe()
        .setTarget("HEAD")
        .setTags(true)
        .call()

gradlePlugin {
    plugins {
        androidEmulatorPlugin {
            id = 'com.quittle.android-emulator'
            implementationClass = 'com.quittle.androidemulator.AndroidEmulatorPlugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'android-emulator'
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/quittle/gradle-android-emulator'
    vcsUrl = 'https://github.com/quittle/gradle-android-emulator'
    description = 'Automatically installs and sets up the Android emulator for easy running of instrumentation tests.'
    tags = ['android', 'android emulator', 'emulator', 'automation', 'instrumentation']

    plugins {
        androidEmulatorPlugin {
            displayName = 'Automated Android Emulator for easing development and testing'
        }
    }
}

dependencies {
    implementation 'com.android.tools.build:gradle:7.1.3'
    implementation 'com.android.tools:common:30.2.2'
    implementation 'commons-io:commons-io:2.8.0'

    compileOnly 'com.google.code.findbugs:annotations:3.0.1'
    testCompileOnly 'com.google.code.findbugs:annotations:3.0.1'

    testImplementation 'io.mockk:mockk:1.12.5'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.7.0'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all' << '-Werror' << '-proc:none'
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

javadoc {
    failOnError true
    options.addStringOption 'Xwerror', '-quiet' // See https://stackoverflow.com/a/49544352

    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
        options.addBooleanOption 'html5', true
    }
}

spotbugs {
    effort 'max'
    reportLevel 'low'
    excludeFilter = file('configuration/spotbugs-exclude.xml')
}

pmd {
    ruleSetFiles = files("configuration/pmd.xml")
    consoleOutput true
    ruleSets = []
}

defaultTasks 'assemble', 'javadoc', 'check'
